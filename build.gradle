plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'kr.co.kjc'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

ext {
    openapi_starter_version = "2.3.0"
    jasypt_version = "3.0.5"
    p6spy_version = "1.9.1"
    apache_poi_xlsx_version = "5.2.5"
    aws_cloud_version = "2.2.6.RELEASE"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.hibernate:hibernate-core:6.2.0.Final'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:'.concat(p6spy_version)
//    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation "com.google.code.gson:gson"
    implementation 'org.springframework.security:spring-security-crypto'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // https://mvnrepository.com/artifact/com.github.ulisesbocchio/jasypt-spring-boot-starter
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:'.concat(jasypt_version)

    // Open API Swagger : https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-ui
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:'.concat(openapi_starter_version)

    // Spring boot 3.x QueryDsl 패키지를 정의
    implementation "com.querydsl:querydsl-jpa:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    implementation "com.querydsl:querydsl-core:${dependencyManagement.importedProperties['querydsl.version']}"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Apache - POI
    implementation 'org.apache.poi:poi-ooxml:'.concat(apache_poi_xlsx_version)

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'

    // Amazon - S3
    implementation "org.springframework.cloud:spring-cloud-starter-aws:".concat(aws_cloud_version)

    // 테스트일 때만 구동
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
//    testImplementation 'io.github.qsy7.java.dependencies:p6spy:0.3.3'
    testImplementation 'org.projectlombok:lombok'

}

tasks.named('test') {
    useJUnitPlatform()
}

// === Querydsl 빌드 옵션 (선택사항) ===
// Plan A
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

configurations {
    querydsl.extendsFrom compileClasspath
}

sourceSets {
    main.java.srcDirs += [querydslDir]
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

clean.doLast {
    file(querydslDir).deleteDir()
}